grammar org.xtext.tdl.Tdl with org.eclipse.xtext.common.Terminals

generate tdl "http://www.xtext.org/tdl/Tdl"

Model:
	compositeTasks += CompositeTask*
	robots += Robot*
;

CompositeTask:
	"compositeTask" name=ID "(lat,lng):"
	BEGIN
		tasks += Task*
	END
;
	
Task:
	"task" name=ID "():"
	BEGIN
		simpleActions += SimpleActionReference*
	END
;

SimpleActionReference:
	name=ID ( "():" | position="(lat,lng):" | "(id="id=INT"):" | "(after("after=INT")):" )  
;


Robot:
	"robot" name=ID ":"
	BEGIN
		parameterList = ParameterList?
		publishedData = PublishedData?
		setupMethod = SetupMethod?
		simpleActions += SimpleAction*
		processingNodes += ProcessingNode*
	END
;

SetupMethod:
	"setup:"
	BEGIN
		codeBlock = CodeBlock
	END
;

SimpleAction:
	"simpleAction" name=ID ("():" | position="(lat,lng):") 
	BEGIN
		codeBlock = CodeBlock
	END
	
;

ProcessingNode:
	(
	"processingNode" name=ID "():"
	BEGIN
		codeBlock = CodeBlock
	END	)
	| 
	("processingNode" name=ID "(" nodeType=STRING "," nodeName=STRING ("," nodeArgs=STRING)? "):"
	(
	BEGIN
		"Parameters:"
		BEGIN
			codeBlock = CodeBlock
		END
	END
	)?
	)
;

ParameterList:
	"ParameterList:"
	BEGIN
		parameter += Parameter*
	END
;

Parameter:
	name = ID ":" value = STRING
;

PublishedData:
	"PublishedData:"
	BEGIN
		sensorData += SensorData*
	END
;

SensorData:
	name = ID ":" topic = STRING
;

CodeBlock hidden():
    (ID | WS | INT | STRING | ML_COMMENT | SL_COMMENT | ANY_OTHER | "(" | ")" | "):" | "()" | ":" | "'" | "," | "))" | CodeBlock_2 )*
;

CodeBlock_2 hidden():
	BEGIN
		CodeBlock
	END
;


terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';


