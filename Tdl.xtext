grammar org.xtext.tdl.Tdl with org.eclipse.xtext.common.Terminals

generate tdl "http://www.xtext.org/tdl/Tdl"

Model:
	tasks += Task*
	robots += Robot*
;
	
Task:
	"task" name=ID "():"
	BEGIN
		simpleActions += SimpleAction*
	END
;

SimpleAction:
	name=ID
;

Robot:
	"robot" name=ID ":"
	BEGIN
		setupMethod = SetupMethod?
		simpleActionDefinitions += SimpleActionDefinition*
		processingNodes += ProcessingNode*
	END
;

SetupMethod:
	"setup:"
	BEGIN
		codeBlock = CodeBlock
	END
;

SimpleActionDefinition:
	(("simpleAction" name=ID "():") | ("simpleAction" name="goTo" ("(lat,lng):" | "():")))
	BEGIN
		codeBlock = CodeBlock
	END
	
;

ProcessingNode:
	"processingNode" name=ID "():"
	BEGIN
		codeBlock = CodeBlock
	END
	
;

CodeBlock hidden():
    (ID | WS | INT | STRING | ML_COMMENT | SL_COMMENT | ANY_OTHER | "(" | ")" | "()" | ":" | "'" | CodeBlock_2 )*
;

CodeBlock_2 hidden():
	BEGIN
		CodeBlock
	END
;


terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';


